apply plugin: 'com.android.application'

android {

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    compileSdkVersion 23
    buildToolsVersion "23.0.1"

    def APP_ID_PREFIX = "com.allco.flickrsearch"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }


    signingConfigs {
        release {
            storeFile file('src/main/app_release.keystore')
            storePassword '123qwe123'
            keyAlias 'app_release'
            keyPassword '123qwe123'
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }



    flavorDimensions 'color'

    productFlavors
            {
                GreenColored
                        {
                            flavorDimension 'color'
                            applicationId APP_ID_PREFIX + ".green"
                        }

                PurpleColored
                        {
                            flavorDimension 'color'
                            applicationId APP_ID_PREFIX + ".purple"
                        }
            }


    sourceSets
            {


                def PATH_FLAVORS = 'src/'
                def path = PATH_FLAVORS + 'Green/';
                GreenColored
                        {
                            res.srcDirs = [path + 'res']
                            java.srcDirs = [path + 'java']
                            assets.srcDirs = [path + 'assets']
                        }

                path = PATH_FLAVORS + 'Purple/';
                PurpleColored
                        {
                            res.srcDirs = [path + 'res']
                            java.srcDirs = [path + 'java']
                            assets.srcDirs = [path + 'assets']
                        }

            }

    applicationVariants.all { variant ->
        boolean isRelease = variant.buildType.name.contains('release')
        String filename = ''

        for (def flavor : variant.productFlavors) {
            def name = flavor.name + '-'
            filename += name
        }

        filename += (isRelease ? 'release' : 'debug')

        variant.outputs.each { output ->
            changeOutputFilename(output, filename, variant.versionName)

            if (output.zipAlign) {
                changeOutputFilename(output, filename, variant.versionName)
            }
        }
    }
}

def changeOutputFilename(outputTask, newNamePrefix, version) {
    def file = outputTask.outputFile

    def suffix = '-v' + version + '.apk'

    def newName = (file.name =~ /\.apk$/).replaceAll('').split('-').drop(4).join('-')
    if (newName.length() > 0) {
        newName = '-' + newName
    }

    newName = newNamePrefix + newName + suffix

    outputTask.outputFile = new File(file.parentFile, newName)
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:23.0.1'
    compile 'com.android.support:design:23.0.1'
    compile 'com.squareup.retrofit:retrofit:2.0.0-beta2'
    compile 'com.squareup.retrofit:converter-gson:2.0.0-beta2'

    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.nhaarman.listviewanimations:lib-core:3.1.0@aar'
    compile 'com.nineoldandroids:library:2.4.0'

    testCompile 'junit:junit:4.12'
    testCompile 'com.squareup.okhttp:mockwebserver:2.5.0'
    testCompile 'org.hamcrest:hamcrest-library:1.3'

    androidTestCompile 'com.android.support.test:runner:0.4'
    androidTestCompile 'com.android.support.test:rules:0.4'
    //compile 'com.android.support:support-annotations:23.1.0'
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.1'

    androidTestCompile 'com.squareup.okhttp:mockwebserver:2.5.0'
}
